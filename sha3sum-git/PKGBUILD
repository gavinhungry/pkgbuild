# Maintainer: Gavin Lloyd <gavinhungry@gmail.com>
# Contributor: onny <onny@project-insanity.org>

pkgname=sha3sum-git
pkgver=0.228.8fc3a72
pkgrel=1
pkgdesc="Checksum utility for Keccak and its close derivatives SHA-3, SHAKE and RawSHAKE"
url="https://github.com/maandree/sha3sum"
license="AGPL3"
arch=('i686' 'x86_64')
makedepends=('auto-auto-complete' 'texman' 'texlive-core')
depends=('argparser' 'libkeccak')
source=('git+https://github.com/maandree/sha3sum')
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/sha3sum"
  echo "0.$(git rev-list --count HEAD).$(git describe --always | sed 's|-|.|g')"
}

build() {
  cd "${srcdir}/sha3sum"
  make default doc
}

package () {
  mkdir -p "${pkgdir}/usr/bin"
  mkdir -p "${pkgdir}/usr/share/man/man1"
  mkdir -p "${pkgdir}/etc/bash_completion.d"

  for P in keccak-224sum keccak-256sum keccak-384sum keccak-512sum keccaksum rawshake256sum rawshake512sum sha3-224sum sha3-256sum sha3-384sum sha3-512sum shake256sum shake512sum; do
    install -m 755 "${srcdir}/sha3sum/bin/${P}" "${pkgdir}/usr/bin/"
    install -m 644 "${srcdir}/sha3sum/bin/${P}.1" "${pkgdir}/usr/share/man/man1/"
    install -m 644 "${srcdir}/sha3sum/bin/${P}.bash" "${pkgdir}/etc/bash_completion.d/"
  done
}
