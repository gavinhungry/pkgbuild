# Maintainer: Gavin Lloyd <gavinhungry@gmail.com>

pkgname=iojs-git
pkgver=0.10360.e7dec60
pkgrel=1
pkgdesc='Evented I/O for V8 JavaScript - Node.js fork'
url='http://iojs.org/'
license=('MIT')
arch=('i686' 'x86_64')
depends=('openssl')
makedepends=('python2' 'git')
provides=('nodejs')
conflicts=('nodejs' 'iojs')
options=('!emptydirs')
source=("$pkgname::git://github.com/iojs/io.js")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  echo "0.$(git rev-list --count HEAD).$(git describe --always | sed 's|-|.|g')"
}

prepare() {
  cd "${srcdir}/${pkgname}"

  find -type f -exec sed \
    -e 's_^#!/usr/bin/env python$_&2_' \
    -e 's_^\(#!/usr/bin/python2\).[45]$_\1_' \
    -e 's_^#!/usr/bin/python$_&2_' \
    -e "s_'python'_'python2'_" -i {} \;
  find test/ -type f -exec sed 's_python _python2 _' -i {} \;
}

build() {
  cd "${srcdir}/${pkgname}"

  export PYTHON=python2
  ./configure \
    --prefix=/usr \
    --shared-openssl

  make
}

package() {
  cd "${srcdir}/${pkgname}"
  make DESTDIR="$pkgdir" install

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/iojs/LICENSE
}
