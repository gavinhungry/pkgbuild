# Contributor: kitsunyan <`echo a2l0c3VueWFuQGFpcm1haWwuY2MK | base64 -d`>
# Maintainer: j-james <jj@j-james.me>

pkgname=pakku
pkgver=0.17
pkgrel=3
pkgdesc='Pacman wrapper and AUR helper with a pacman-like user interface'
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url="https://github.com/zqqw/$pkgname"
license=('GPL3')
depends=('libcurl.so' 'git' 'pcre')
makedepends=('nim' 'git' 'asciidoc')
backup=('etc/pakku.conf')
source=("$url/archive/refs/heads/master.zip")
sha256sums=('36910b9bd9d36e165c574dcd44c85405a4d83f8803b8a4c15030e169854c407c')

build() {
  local addargs=()
  grep -Fxq debug <<< "`printf '%s\n' "${options[@]}"`" &&
  addargs=(NIM_TARGET='debug' NIM_OPTIMIZE='none')

  cd "$srcdir/$pkgname-master"
  make "${addargs[@]}" NIM_CACHE_DIR='../nimcache' PREFIX='/usr'
}

package() {
  cd "$srcdir/$pkgname-master"
  make PREFIX='/usr' DESTDIR="$pkgdir" install
}

[[ "$PACKAGER" != *"[p]" ]] && PACKAGER+=" [p]" || true
